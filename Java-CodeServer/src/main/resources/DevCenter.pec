define dialect as storable Text attribute
define body as storable Text attribute

define Declaration as storable category with attributes name, version, dialect and body

define memberDeclarations as storable Declaration[] attribute

define Project as storable category with attributes name, version, description, image and memberDeclarations

define Library as Project
define Service as Project
define Script as Project

define entryPoint as storable Text attribute

define Application as Project with attribute entryPoint

define project as storable Project attribute
define lastOpened as storable DateTime attribute

define CodingSession as storable category with attributes user, project and lastOpened
define RecentProject as storable CodingSession

define getRecentProjects as method receiving Integer count doing:
	user = getHttpUser
	sessions = fetch RecentProject 1 to count where user = user order by lastOpened desc
	return session.project for each session in sessions

define getAllProjects as method doing:
	return fetch all Project order by name

define getProjectDeclarations as method receiving dbId doing:
	user = getHttpUser
	project = fetch one Project where dbId = dbId
	// use this opportunity to keep track of coding sessions
	session = CodingSession with user as user, project as project and now as lastOpened 
	store session
	// use this opportunity to keep track of recent projects
	recent = fetch one mutable RecentProject where user = user and project = project
	if recent is nothing:
		recent = RecentProject with user as user, project as project and now as lastOpened 
	else:
		recent.lastOpened = now 
	store recent
	
	return project.memberDeclarations